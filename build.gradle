plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.2.2'
    id 'io.spring.dependency-management' version '1.1.4'
    id "org.sonarqube" version "4.4.1.3373"
    id 'idea'
}

group = 'uk.co.dajohnston'
version = '0.0.2'

java {
    sourceCompatibility = '21'
}

tasks.named("jar") {
    enabled = false
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestImplementation {
        extendsFrom testImplementation
    }
    integrationTestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.security:spring-security-oauth2-resource-server'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.mockito:mockito-core:5.9.0"
    testImplementation "org.mockito:mockito-junit-jupiter:5.9.0"
}

tasks.named('test') {
    useJUnitPlatform()
}
test {
    ignoreFailures Boolean.getBoolean("test.ignoreFailures")
    finalizedBy jacocoTestReport
}
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    ignoreFailures Boolean.getBoolean("test.ignoreFailures")
    finalizedBy jacocoTestReport

    useJUnitPlatform()
}
jacocoTestReport {
    executionData(integrationTest)
    reports {
        xml.required = true
    }
}

check.dependsOn integrationTest

idea {
    module {
        testSources.from(sourceSets.integrationTest.java.srcDirs)
        downloadJavadoc = true
        downloadSources = true
    }
}
