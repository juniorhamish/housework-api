plugins {
    id 'java'
    id 'java-test-fixtures'
    id 'jacoco'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.sonarqube" version "5.1.0.4882"
    id 'idea'
}

group = 'uk.co.dajohnston'
version = '0.0.3'

java {
    sourceCompatibility = '22'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testFixturesCompileOnly {
        extendsFrom testFixturesAnnotationProcessor
    }
    integrationTestImplementation {
        extendsFrom testImplementation
    }
    integrationTestRuntimeOnly {
        extendsFrom testRuntimeOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.logtail:logback-logtail:0.3.4'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0'
    implementation 'org.mapstruct:mapstruct:1.6.0'
    implementation 'io.micrometer:micrometer-tracing'
    implementation 'io.micrometer:micrometer-tracing-bridge-otel'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation "org.mockito:mockito-core:5.14.1"
    testImplementation "org.mockito:mockito-junit-jupiter:5.13.0"
    testFixturesAnnotationProcessor 'org.projectlombok:lombok'
    testFixturesImplementation 'org.springframework.security:spring-security-test'
    testFixturesImplementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.suppressGeneratorTimestamp=true',
            '-Amapstruct.suppressGeneratorVersionInfoComment=true',
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.defaultInjectionStrategy=constructor'
    ]
}

tasks.named('test') {
    useJUnitPlatform()
}
test {
    ignoreFailures Boolean.getBoolean("test.ignoreFailures")
    finalizedBy jacocoTestReport
}
tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    ignoreFailures Boolean.getBoolean("test.ignoreFailures")
    finalizedBy jacocoTestReport

    useJUnitPlatform()
}
jacocoTestReport {
    executionData(integrationTest)
    reports {
        xml.required = true
    }
}

check.dependsOn integrationTest

idea {
    module {
        testSources.from(sourceSets.integrationTest.java.srcDirs)
        downloadJavadoc = true
        downloadSources = true
    }
}
